#!/usr/bin/perl

use Mail::ExpandAliases;
use File::Basename;
use POSIX qw(getgroups);

use strict;
use warnings;

system(qw(klist -s));
my $kuser;
my $want_auth = $ENV{'DEBATHENA_SENDMAIL_AUTH'} || '';

if (($? == 0) &&
    (`klist 2>/dev/null` =~ /Default principal: (.*?)\@ATHENA.MIT.EDU/)) {
    $kuser = $1;
}

my $parser = Mail::ExpandAliases->new;

if (basename($0) eq 'newaliases') {
    my $root = join(', ', @{$parser->expand('root')});
    if ($root !~ /@/) {
	print STDERR <<EOF
NOTE: root expands to: $root
This does not appear to contain a remote address.  Since debathena-msmtp
does not support local delivery, you may wish to send root's mail
somewhere useful (e.g. your MIT account).
EOF
    }
    exit 0;
}

sub from_address {
  # If we have tickets, use them
  if ($kuser) {
    return "--from=" . join('@', $kuser, 'mit.edu');
  }
  my $uname = $ENV{'USER'} || $ENV{'LOGNAME'} || getlogin();
  # Otherwise, assume user@fqdn ...
  chomp(my $maildomain = `hostname --fqdn`);
  # ... except that nss-nonlocal-users are @mit.edu
  if (getgrnam('nss-nonlocal-users')) {
    my $nssnonlocalgid = (getgrnam('nss-nonlocal-users'))[2];
    if (grep(/^$nssnonlocalgid$/, getgroups())) {
      $maildomain = 'mit.edu';
    }
  }
  return "--from=" . join('@', $uname, $maildomain);
}

my @aliases = ();
foreach my $arg (@ARGV) {
    push @aliases, $parser->expand($arg);
}

if ($kuser && (($want_auth eq 'yes') || ($want_auth eq '')) ) {
    #send auth
    exec(qw{msmtp --host=outgoing.mit.edu --port=587 --auth=gssapi}, "--user=$kuser", from_address(), @aliases);
}
elsif ($want_auth eq 'yes') {
    $! = 1;
    die "Could not find valid ATHENA.MIT.EDU Kerberos tickets.\n";
}
else {
    #send unauth
    exec(qw{msmtp --host=outgoing.mit.edu --port=25 --auth=off}, from_address(), @aliases);
}
